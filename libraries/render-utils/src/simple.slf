<@include gpu/Config.slh@>
<$VERSION_HEADER$>
//  Generated on <$_SCRIBE_DATE$>
//
//  simple.frag
//  fragment shader
//
//  Created by Andrzej Kapolka on 9/15/14.
//  Copyright 2014 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//

<@include DeferredBufferWrite.slh@>

<@include render-utils/ShaderConstants.h@>

// the interpolated normal
layout(location=RENDER_UTILS_ATTR_NORMAL_WS) in vec3 _normalWS;
layout(location=RENDER_UTILS_ATTR_NORMAL_MS) in vec3 _normalMS;
layout(location=RENDER_UTILS_ATTR_COLOR) in vec4 _color;
layout(location=RENDER_UTILS_ATTR_TEXCOORD01) in vec4 _texCoord01;
#define _texCoord0 _texCoord01.xy
#define _texCoord1 _texCoord01.zw
layout(location=RENDER_UTILS_ATTR_POSITION_MS) in vec4 _positionMS;
layout(location=RENDER_UTILS_ATTR_POSITION_ES) in vec4 _positionES;

// For retro-compatibility
#define _normal      _normalWS
#define _modelNormal _normalMS
#define _position    _positionMS
#define _eyePosition _positionES

<@include procedural/ProceduralCommon.slh@>

#line 1001
//PROCEDURAL_BLOCK

#line 2030
void main(void) {
    vec3 normal = normalize(_normalWS.xyz);
    vec3 diffuse = _color.rgb;
    vec3 specular = DEFAULT_SPECULAR;
    float shininess = DEFAULT_SHININESS;
    float emissiveAmount = 0.0;

#ifdef PROCEDURAL

#if defined(PROCEDURAL_V1)
    diffuse = getProceduralColor().rgb;
    // Procedural Shaders are expected to be Gamma corrected so let's bring back the RGB in linear space for the rest of the pipeline
    //diffuse = pow(diffuse, vec3(2.2));
    emissiveAmount = 1.0;
#elif defined(PROCEDURAL_V2)
    emissiveAmount = getProceduralColors(diffuse, specular, shininess);
#elif defined(PROCEDURAL_V3)
    TransformCamera cam = getTransformCamera();
    vec4 position = cam._viewInverse * _positionES;
    ProceduralData proceduralData = {
        position.xyz,
        normal,
        vec3(0.0),
        DEFAULT_SPECULAR,
        DEFAULT_EMISSIVE,
        1.0,
        DEFAULT_ROUGHNESS,
        DEFAULT_METALLIC,
        DEFAULT_OCCLUSION,
        DEFAULT_SCATTERING
    };
    emissiveAmount = getProceduralColors(proceduralData);
    if (emissiveAmount > 0.0) {
        packDeferredFragmentLightmap(
            proceduralData.normal,
            1.0,
            proceduralData.diffuse,
            proceduralData.roughness,
            proceduralData.metallic,
            proceduralData.specular,
            proceduralData.emissive);
    } else {
        packDeferredFragment(
            proceduralData.normal,
            1.0,
            proceduralData.diffuse,
            proceduralData.roughness,
            proceduralData.metallic,
            proceduralData.emissive,
            proceduralData.occlusion,
            proceduralData.scattering);
    }
    position = vec4(proceduralData.position, 1.0);
    vec4 posClip = cam._projection * (cam._view * position);
    float far = gl_DepthRange.far;
    float near = gl_DepthRange.near;
    gl_FragDepth = 0.5 * ((far - near) * posClip.z / posClip.w + near + far);
#endif

#endif

#ifndef PROCEDURAL_V3
    if (emissiveAmount > 0.0) {
        packDeferredFragmentLightmap(
            normal,
            1.0,
            diffuse,
            max(0.0, 1.0 - shininess / 128.0),
            DEFAULT_METALLIC,
            specular,
            vec3(clamp(emissiveAmount, 0.0, 1.0)));
    } else {
        packDeferredFragment(
            normal,
            1.0,
            diffuse,
            max(0.0, 1.0 - shininess / 128.0),
            length(specular),
            DEFAULT_EMISSIVE,
            DEFAULT_OCCLUSION,
            DEFAULT_SCATTERING);
    }
#endif

}
